desc: SoX Bass/Treble Filter
tags: filter
author: Dr. Thomas Tensi, 2019

//--------------------
// IMPORTS
//--------------------

import ../_BaseModules/Base.jsfx-inc
import ../_BaseModules/Base_memory.jsfx-inc
import ../_BaseModules/Base_naturalset.jsfx-inc
import ../_BaseModules/Base_string.jsfx-inc
import ../_BaseModules/Base_fixedarray.jsfx-inc
import ../_BaseModules/Base_struct.jsfx-inc
import ../_BaseModules/Base_graphics.jsfx-inc
import ../_BaseModules/Base_debug.jsfx-inc

import ../_Audio/Audio_samplequeue.jsfx-inc

import SoX__base.jsfx-inc
import SoX__iirfilter.jsfx-inc
import SoX__biquadfilter.jsfx-inc

//--------------------
// SLIDERS
//--------------------

slider1:SL_mode=0<0,1,1{Bass,Treble}>Mode
slider2:SL_dBFilterGain=0<-25,25,0.05>Gain [dB]
slider3:SL_frequency=1000.0<10.0,20000.0,1>Frequency [Hz]
slider4:SL_bandwidth=1<0.001,20000,0.00001>Bandwidth
slider5:SL_bandwidthKind=0<0,4,1{Frequency,Octaves,Q,Butterworth,Slope}>Bandwidth Kind

//--------------------
// INITIALIZATION
//--------------------

@init

    function SoXBsTrbFilter_update ()
        /** Updates filter parameters from sliders */
        local (a a0 a1 a2 alpha b0 b1 b2 w0)
    (
        a = 10^(SL_dBFilterGain/40);

        // coefficients for biquad transform
        w0 = twoPi * SL_frequency / srate;
        alpha =
            SoXBiquadFilter_alphaForBandwidth(SL_bandwidth,
                                              SL_bandwidthKind,
                                              SL_frequency,
                                              SL_dBFilterGain);

        SL_mode == SoXBsTrb_bassFilter ? (
            // bass filter => low shelf
            b0 =      a * ( (a+1) - (a-1)*cos(w0) + 2*sqrt(a)*alpha );
            b1 =  2 * a * ( (a-1) - (a+1)*cos(w0)                   );
            b2 =      a * ( (a+1) - (a-1)*cos(w0) - 2*sqrt(a)*alpha );
            a0 =            (a+1) + (a-1)*cos(w0) + 2*sqrt(a)*alpha;
            a1 = -2 *     ( (a-1) + (a+1)*cos(w0)                   );
            a2 =            (a+1) + (a-1)*cos(w0) - 2*sqrt(a)*alpha;
        ) : (
            // treble filter => high shelf
            b0 =      a * ( (a+1) + (a-1)*cos(w0) + 2*sqrt(a)*alpha );
            b1 = -2 * a * ( (a-1) + (a+1)*cos(w0)                   );
            b2 =      a * ( (a+1) + (a-1)*cos(w0) - 2*sqrt(a)*alpha );
            a0 =            (a+1) - (a-1)*cos(w0) + 2*sqrt(a)*alpha;
            a1 =  2 *     ( (a-1) - (a+1)*cos(w0)                   );
            a2 =            (a+1) - (a-1)*cos(w0) - 2*sqrt(a)*alpha;
        );

        SoXBiquadFilter_init(biquadFilter, b0, b1, b2, a0, a1, a2);
        Debug_clear();
        SoXBiquadFilter_print(biquadFilter);
    );

    //============================================================

    function SoXBsTrbFilter_handleKey (key)
    (
        Debug_currentMode == Debug_Mode_effect ? (
            Graphics_clear(Graphics_Color_black);
        );
    );

    //============================================================

    SoXBsTrb_bassFilter = 0;

//--------------------
// BLOCK
//--------------------

@block
    srate != SoXBiquadFilter__previousSampleRate ? (
        SoXBsTrbFilter_update();
        SoXBiquadFilter__previousSampleRate = srate;
    );

//--------------------
// SLIDER CHANGE
//--------------------

@slider
    SoXBsTrbFilter_update();

//--------------------
// GRAPHICS
//--------------------

@gfx
    key = gfx_getchar();
    key = Debug_handleKey(key);
    SoXBsTrbFilter_handleKey(key);
