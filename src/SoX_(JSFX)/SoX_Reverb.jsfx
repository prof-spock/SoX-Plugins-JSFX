desc: SoX Reverb
tags: reverb freeverb
author: Dr. Thomas Tensi, 2019

//--------------------
// SLIDERS
//--------------------

slider1:SL_wetOnly=0<0,1,1{WetAndDry,WetOnly}>is wet only?
slider2:SL_reverberanceInPercent=50<0,100,1>Reverberance [%]
slider3:SL_hfDampingInPercent=50<0,100,1>HF Damping [%]
slider4:SL_roomScaleInPercent=100<0,100,1>Room Scale [%]
slider5:SL_stereoDepthInPercent=100<0,100,1>Stereo Depth [%]
slider6:SL_predelayInMilliseconds=0<0,500,0.001>Pre Delay [ms]
slider7:SL_wetDbGain=0<-10,10,0.01>Wet Gain [dB]

//--------------------
// IMPORTS
//--------------------

import ../_BaseModules/Base.jsfx-inc
import ../_BaseModules/Base_memory.jsfx-inc
import ../_BaseModules/Base_naturalset.jsfx-inc
import ../_BaseModules/Base_string.jsfx-inc
import ../_BaseModules/Base_fixedarray.jsfx-inc
import ../_BaseModules/Base_struct.jsfx-inc
import ../_BaseModules/Base_graphics.jsfx-inc
import ../_BaseModules/Base_debug.jsfx-inc

import ../_Audio/Audio_samplequeue.jsfx-inc

import SoX__base.jsfx-inc
import SoX__reverb.jsfx-inc

//--------------------
// INITIALIZATION
//--------------------

@init

    //============================================================

    function SoXReverb_handleKey (self, key)
    (
        Debug_currentMode == Debug_Mode_effect ? (
            Graphics_clear(Graphics_Color_black);
        );
    );

    //--------------------

    function SoXReverb_reset ()
        /** Releases all resources for current reverb effect */
    (
        SoX_resetBaseLibraries();
        SoXReverb_initialize();
    );

    //--------------------

    function SoXReverb_update (self)
        /** Updates on change of play position */
    (
        SoXReverb_init(self,
                       (SL_wetOnly == 1), SL_reverberanceInPercent,
                       SL_hfDampingInPercent, SL_roomScaleInPercent,
                       SL_stereoDepthInPercent,
                       SL_predelayInMilliseconds / 1000,
                       SL_wetDbGain);
        Debug_clear();
        SoXReverb_print(self, 0);
    );

    //============================================================

    SoXReverb_initialize();
    reverb = SoXReverb_make();

//--------------------
// BLOCK
//--------------------

@block
    srate != SoXReverb__previousSampleRate ? (
        SoXReverb_reset();
        reverb = SoXReverb_make();
        SoXReverb_update(reverb);
        SoXReverb__previousSampleRate = srate;
    );

//--------------------
// SLIDER CHANGE
//--------------------

@slider
    SoXReverb_update(reverb);

//--------------------
// SAMPLE
//--------------------

@sample
    SoX_readInputSampleList();
    SoXReverb_apply(reverb, SoX_inputSampleList, SoX_outputSampleList);
    SoX_writeOutputSampleList();

//--------------------
// GRAPHICS
//--------------------

@gfx
    key = gfx_getchar();
    key = Debug_handleKey(key);
    SoXReverb_handleKey(reverb, key);
