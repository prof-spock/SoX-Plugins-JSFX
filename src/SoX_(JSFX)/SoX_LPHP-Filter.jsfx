desc: SoX Lowpass/Highpass Filter
tags: filter
author: Dr. Thomas Tensi, 2019

//--------------------
// IMPORTS
//--------------------

import ../_BaseModules/Base.jsfx-inc
import ../_BaseModules/Base_memory.jsfx-inc
import ../_BaseModules/Base_naturalset.jsfx-inc
import ../_BaseModules/Base_string.jsfx-inc
import ../_BaseModules/Base_fixedarray.jsfx-inc
import ../_BaseModules/Base_struct.jsfx-inc
import ../_BaseModules/Base_graphics.jsfx-inc
import ../_BaseModules/Base_debug.jsfx-inc

import ../_Audio/Audio_samplequeue.jsfx-inc

import SoX__base.jsfx-inc
import SoX__iirfilter.jsfx-inc
import SoX__biquadfilter.jsfx-inc

//--------------------
// SLIDERS
//--------------------

slider1:SL_mode=0<0,1,1{Highpass,Lowpass}>Mode
slider2:SL_poles=0<0,1,1{Single,Double}>Poles
slider3:SL_frequency=1000.0<10.0,20000.0,1>Frequency [Hz]
slider4:SL_bandwidth=1.0<0.001,20000,0.00001>Bandwidth
slider5:SL_bandwidthKind=0<0,3,1{Frequency,Octaves,Q,Butterworth}>Bandwidth Kind

//--------------------
// INITIALIZATION
//--------------------

@init

    function SoXLpHpFilter_update ()
        /** Updates filter parameters from sliders */
        local (a0 a1 a2 alpha b0 b1 b2 factorA factorB factorC
               poles w0)
    (
        // coefficients for biquad transform
        w0 = twoPi * SL_frequency / srate;
        alpha =
            SoXBiquadFilter_alphaForBandwidth(SL_bandwidth,
                                              SL_bandwidthKind,
                                              SL_frequency,
                                              0);

        SL_poles == SoXLpHp_singlePole ? (
            SL_mode == SoXLpHp_highPassFilter ? (
                // highpass
                factorA = -1;
                factorB = 0.5;
                factorC = -1;
            ) : (
                // lowpass
                factorA = 1;
                factorB = 1;
                factorC = 0;
            );

            a0 = 1;
            a1 = -exp(-w0);
            a2 = 0;
            b0  = (1 + factorA * a1) * factorB;
            b1  = factorC * b0;
            b2  = 0;
        ) : (
            SL_mode == SoXLpHp_highPassFilter ? (
                // highpass
                factorA = (1 + cos(w0));
                factorB = -1;
            ) : (
                // lowpass
                factorA = (1 - cos(w0));
                factorB = 1;
            );

            b0  = factorA / 2.0;
            b1  = factorB * factorA;
            b2  = b0;
            a0 = 1 + alpha;
            a1 = -2 * cos(w0);
            a2 = 1 - alpha;
        );

        SoXBiquadFilter_init(biquadFilter, b0, b1, b2, a0, a1, a2);
        Debug_clear();
        SoXBiquadFilter_print(biquadFilter);
    );

    //============================================================

    function SoXLpHp_handleKey (key)
    (
        Debug_currentMode == Debug_Mode_effect ? (
            Graphics_clear(Graphics_Color_black);
        );
    );

    //============================================================

    SoXLpHp_highPassFilter = 0;
    SoXLpHp_singlePole = 0;

//--------------------
// BLOCK
//--------------------

@block
    srate != SoXBiquadFilter__previousSampleRate ? (
        SoXLpHpFilter_update();
        SoXBiquadFilter__previousSampleRate = srate;
    );

//--------------------
// SLIDER CHANGE
//--------------------

@slider
    SoXLpHpFilter_update();

//--------------------
// GRAPHICS
//--------------------

@gfx
    key = gfx_getchar();
    key = Debug_handleKey(key);
    SoXLpHp_handleKey(key);
